10 REM OTHELL
20 REM
30 REM MSX Computer Magazine #16, 1987
40 REM
50 REM Submitted by Rien van Doorn, Krommenie
60 REM
70  '*********** OTHELLO ***********
80  '
90  '** Author: Rien van Doorn    **
100 '
110 '************ intro ************
120 SCREEN 0:WIDTH 37:KEYOFF:PRINTTAB(11)"O T H E L L O":PRINT
130 PRINT"The object of the game is to conquer most of the board's squares. The   "
140 PRINT"squares dominated by the opponent    are reverted to you as soon as you"
150 PRINT"enclose his pieces with yours.       The game ends when the board is "
160 PRINT"full or enclosures are no more       possible."
170 PRINT"The winner is the players with       more squares conquered.":PRINT
180 PRINT"Do you need help?"
190 PRINT"Case affirmative, type 1.":PRINT:PRINT"Choose level: [1] beginners"
200 PRINTTAB(14)"[2] advanced":PRINT
210 K$=INPUT$(1):K=VAL(K$):IF K<1 OR K>2 THEN 210
220 IF K=1 THEN S1=0:S$=" beginners"ELSE S1=1:S$="advanced"
230 LOCATE 0,17:PRINT"***** "S$"-level selected *****"SPACE$(37)
240 PRINT"Do you want to make the first move ? (y/n)":PRINT
250 K$=INPUT$(1):IF INSTR("Yy",K$)THEN SS=1 ELSE IF INSTR("Nn",K$)THEN SS=0 ELSE 250
260 '
270 '************ board ************
280 CLS:FOR I=8 TO 1 STEP-1:PRINT RIGHT$(STR$(I),1);:FOR J=1 TO 8
290 PRINT" "+CHR$(1)+CHR$(7+64)+" ";:NEXT J:PRINT:PRINT:NEXT I:PRINT"  A  B  C  D  E  F  G  H"
300 LOCATE 28,0:PRINT"OTHELLO":LOCATE 27,3:PRINT"x :me   2"
310 LOCATE 27,5:PRINT"o :you  2":LOCATE 26,16:PRINT S$
320 '
330 '******* starting positions ********
340 T%(5,5)=-1:T%(6,5)=1:LOCATE 11,6:PRINT"x  o"
350 T%(5,6)=1:T%(6,6)=-1:LOCATE 11,8:PRINT"o  x"
360 FOR I=2 TO 9:FOR J=2 TO 9:READ D%(I,J):NEXT J,I:IF SS=1 THEN 660
370 DATA 15,1,10,11,11,10,1,15,1,0,9,5,5,9,0,1
380 DATA 10,9,7,7,7,7,9,10,11,5,7,6,6,7,5,11
390 DATA 11,5,7,6,6,7,5,11,10,9,7,7,7,7,9,10
400 DATA 1,0,9,5,5,9,0,1,15,1,10,11,11,10,1,15
410 '
420 '****** computer turn ******
430 U1=-1:U2=1:M(0)=-99:M(1)=0:M(2)=0
440 FOR P=8 TO 12 STEP 2:LOCATE 27,P:PRINT SPACE$(10):NEXT P
450 LOCATE 27,8:PRINT"My turn":LOCATE 27,10:PRINT"Wait !"
460 FOR X=2 TO 9:FOR Y=2 TO 9:IF T%(X,Y)=0 THEN GOSUB 950
470 M(4)=0:NEXT Y,X:IF M(0)=-99 THEN 590
480 F1=1:X=M(1):Y=M(2):T%(X,Y)=-1:LOCATE 27,10:PRINT"I place "CHR$(63+X)CHR$(58-Y)" "
490 FOR W=1 TO 50:BEEP:NEXT W:FOR L=1 TO 8:IF T%(X,Y)=1 THEN T%(X,Y)=-1 ELSE T%(X,Y)=1
500 LOCATE 3*X-4,2*Y-4:IF T%(X,Y)>0 THEN PRINTCHR$(1)+CHR$(7+64)ELSE PRINT"x":BEEP
510 FOR W=1 TO 100:NEXT W,L:GOSUB 950:NC=0:NP=0:FOR I=2 TO 9:FOR J=2 TO 9
520 IF T%(I,J)=-1 THEN NC=NC+1 ELSE IF T%(I,J)=1 THEN NP=NP+1
530 NEXT J,I
540 LOCATE 34,3:PRINT USING"##";NC:LOCATE 34,5:PRINT USING"##";NP:F2=0
550 NT=NP+NC:LOCATE 8,20
560 IF NP=0 OR(NT=64 AND NC>NP)THEN PRINT"I have won !":GOTO 610
570 IF NC=0 OR(NT=64 AND NP>NC)THEN PRINT"You have won !":GOTO 610
580 IF NT=64 AND NC=NP THEN PRINT"Drawn !":GOTO 610 ELSE 660
590 LOCATE 27,10:PRINT"Cannot !":F2=1:IF F3=1 THEN 870
600 FOR W=1 TO 50:BEEP:NEXT W:GOTO 550
610 PLAY"O4L12cL4eff#L6gL12aL6bO5L4c","O4L12cO3L4bagL6fL12eL6dL4c"
620 LOCATE 8,22:PRINT"Once more ? (y/n) ";:K$=INPUT$(1)
630 IF INSTR("Yy",K$)THEN RUN ELSE IF INSTR("Nn",K$)THEN CLS:KEYON:END ELSE 610
640 '
650 '******* player turn *******
660 U1=1:U2=-1:L1=0:M(0)=-99:M(4)=0:F3=0
670 FOR P=8 TO 12 STEP 2:LOCATE 27,P:PRINT SPACE$(10):NEXT P
680 LOCATE 27,8:PRINT"Your turn":LOCATE 27,10:PRINT"Where ? ";
690 I$=INPUT$(1):IF I$="?"THEN 820
700 X=(ASC(I$)AND &HDF)-63:IF X<2 OR X>9 THEN 660 ELSE PRINT I$;
710 J$=INPUT$(1):Y=10-VAL(J$):IF Y<2 OR Y>9 THEN 710 ELSE PRINT J$
720 GOSUB 950:IF L1=1 THEN 740
730 LOCATE 27,12:PRINT"Cannot !":FOR W=1 TO 50:BEEP:NEXT W:GOTO 660
740 F1=1:T%(X,Y)=1:FOR L=1 TO 8:IF T%(X,Y)=1 THEN T%(X,Y)=-1 ELSE T%(X,Y)=1
750 LOCATE 3*X-4,2*Y-4:IF T%(X,Y)<0 THEN PRINTCHR$(1)+CHR$(7+64)ELSE PRINT"o":BEEP
760 FOR W=1 TO 100:NEXT W,L:GOSUB 950:NC=0:NP=0:FOR I=2 TO 9:FOR J=2 TO 9
770 IF T%(I,J)=-1 THEN NC=NC+1 ELSE IF T%(I,J)=1 THEN NP=NP+1
780 NEXT J,I
790 LOCATE 34,3:PRINT USING"##";NC:LOCATE 34,5:PRINT USING"##";NP:GOTO 430
800 '
810 '***** help from the computer ******
820 LOCATE 27,10:PRINT"Wait !"
830 FOR X=2 TO 9:FOR Y=2 TO 9:IF T%(X,Y)=0 THEN GOSUB 950
840 M(4)=0:IF L1=1 AND S1=1 THEN 890
850 NEXT Y,X:IF L1=1 THEN 890
860 LOCATE 27,10:PRINT"Cannot !":F3=1
870 IF F2=1 THEN LOCATE 27,12:PRINT"Beide vast":NT=64:LOCATE 8,20:GOTO 560
880 FOR W=1 TO 50:BEEP:NEXT W:GOTO 430
890 LOCATE 27,10:PRINT"Attention!"
900 LOCATE 27,12:PRINT CHR$(63+M(1))CHR$(58-M(2));
910 IF S1=1 THEN PRINT" could be"ELSE PRINT" good"
920 FOR W=1 TO 50:BEEP:NEXT W:GOTO 660
930 '
940 '** subr.1 ingesloten velden **
950 IF T%(X,Y)<>0 AND F1=0 THEN RETURN
960 XX=X:YY=Y:FOR I=-1 TO 1:FOR J=-1 TO 1:N=1:IF T%(X+I,Y+J)<>U2 THEN 1140
970 N=N+1:A=X+(N-1)*I:B=Y+(N-1)*J:IF A<2 OR A>9 OR B<2 OR B>9 THEN 1140
980 IF T%(A,B)=U2 THEN 970 ELSE IF T%(A,B)=0 THEN 1140
990 M(4)=M(4)+N-2:IF F1<>1 THEN 1030
1000 FOR K=1 TO N-2:X=M(1)+K*I:Y=M(2)+K*J:T%(X,Y)=U1
1010 LOCATE 3*X-4,2*Y-4:IF T%(X,Y)>0 THEN PRINT"o"ELSE PRINT"x"
1020 BEEP:NEXT K
1030 X=XX:Y=YY:IF M(4)=NP AND S1=1 AND U1=-1 THEN D%(X,Y)=20
1040 IF D%(X,Y)=1 AND(Y=2 OR Y=9)AND J=0 AND U1=-1 THEN D%(X,Y)=12
1050 IF D%(X,Y)=1 AND(X=2 OR X=9)AND I=0 AND U1=-1 THEN D%(X,Y)=12
1060 IF D%(X,Y)<M(0)THEN 1140
1070 IF D%(X,Y)=M(0)AND M(4)=M(5)THEN IF RND(0)>.5 THEN 1140
1080 IF D%(X,Y)=M(0)AND M(4)<M(5)THEN 1140
1090 IF U1=1 OR S1=0 THEN 1120
1100 IF D%(X,Y)>9 THEN GOSUB 1170
1110 IF DF=1 THEN DF=0:GOTO 1130
1120 M(0)=D%(X,Y):M(1)=X:M(2)=Y:M(5)=M(4)
1130 L1=1
1140 NEXT J,I:F1=0:RETURN
1150 '
1160 '** subr.2 ingesloten velden **
1170 IF D%(X,Y)<>15 THEN 1210
1180 FOR II=-1 TO 1:FOR JJ=-1 TO 1:D%(X+II,Y+JJ)=D%(X+II,Y+JJ)+8:NEXT JJ,II
1190 IF X=Y THEN D%(4,4)=8:D%(5,5)=8:D%(6,6)=8:D%(7,7)=8:RETURN
1200 D%(4,7)=8:D%(5,6)=8:D%(6,5)=8:D%(7,4)=8:RETURN
1210 IF D%(X,Y)>10 THEN 1270
1220 A=8:B=8:C=9:D=9:E=9:F=9:IF X<5 THEN A=3:C=2:E=2
1230 IF X<3 THEN E=4 ELSE IF X>7 THEN E=7
1240 IF Y<5 THEN B=3:D=2:F=2
1250 IF Y<3 THEN F=4 ELSE IF Y>7 THEN F=7
1260 IF T%(A,B)=U2 AND T%(C,D)<>U1 AND T%(E,F)<>0 THEN DF=1:RETURN
1270 FOR K=3 TO 7:IF X=2 THEN D%(K,Y)=0 ELSE IF X=9 THEN D%(K+1,Y)=0
1280 IF Y=2 THEN D%(X,K)=8 ELSE IF Y=9 THEN D%(X,K+1)=8
1290 NEXT K:RETURN
